#
# ~/.zshrc
#

export LANG=en_US.UTF-8
export LANGUAGE=en_US

bindkey -e

# {{{ command history
HISTFILE=~/.zsh_history
HISTSIZE=1000
SAVEHIST=50000
HISTORY_IGNORE="((la|ll|ls|lla|popd)|(cd|la|ll|ls|lla|man|scp|sftp|ssh|sudo|rm) *)"
zshaddhistory() {
	whence ${${(z)1}[1]} >| /dev/null || return 1
}
setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_no_store
# }}}

setopt autocd
setopt auto_pushd
setopt pushd_ignore_dups

setopt no_beep

autoload -Uz select-word-style && select-word-style bash

# {{{ syntax highlight
highlight_src=/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
[[ -f $highlight_src ]] && . "$highlight_src"
unset highlight_src
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[globbing]='none'
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
# }}}

# {{{ load other files
[[ -f ~/.zsh_aliases ]] && . ~/.zsh_aliases
[[ -f ~/.zsh_keys ]] && . ~/.zsh_keys
[[ -f ~/.zsh_prompt ]] && . ~/.zsh_prompt

if [[ -d ~/.asdf ]]; then
	. ~/.asdf/asdf.sh
	fpath=(~/.asdf/completions $fpath)
fi
# }}}

# {{{ PATH setting
[[ -d ~/.local/flutter ]] && path=(~/.local/flutter/default/bin $path)
hash go 2>/dev/null && path=($(go env GOPATH)/bin $path)
[[ -d ~/.local/bin ]] && path=(~/.local/bin $path)

# flutter version switcher
swflutter() {
	if [[ $# -eq 0 ]]; then
		echo missing version >&2
		return 1
	fi
	local flutter_dir=~/.local/flutter
	local new_ver="$1"
	if [[ ! -d $flutter_dir/$new_ver ]]; then
		echo unknown version >&2
		return 1
	fi
	rm "$flutter_dir/default"
	ln -sf "$new_ver" "$flutter_dir/default"
}
# }}}

# {{{ auto completion
fpath+=~/.zfunc
autoload -Uz compinit
compinit
hash pip 2>/dev/null && eval "$(pip completion --zsh)"
# }}}
